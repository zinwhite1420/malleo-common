allprojects {
    group = "com.malleo"
    version = findProperty("projectVersion") ?: "0.1.0-SNAPSHOT"

    repositories {
        mavenCentral()
    }
}

subprojects { sub ->
    apply plugin: "maven-publish"

    if (name == "common-bom") {
        apply plugin: "java-platform"
    } else {
        apply plugin: "java-library"
    }

    sub.plugins.withType(JavaPlugin).configureEach {
        extensions.configure(JavaPluginExtension) { ext ->
            def javaVer = (sub.findProperty("javaVersion") ?: "25").toString().toInteger()
            ext.toolchain.languageVersion = JavaLanguageVersion.of(javaVer)
        }

        dependencies {
            add("testImplementation", "org.junit.jupiter:junit-jupiter")
            add("testRuntimeOnly", "org.junit.platform:junit-platform-launcher")
        }

        tasks.withType(Test).configureEach {
            useJUnitPlatform()
        }
    }

    if (sub.name != "common-bom") {
        sub.dependencies {
            add("api", platform(project(":common-bom")))
            add("testImplementation", platform(project(":common-bom")))
            add("annotationProcessor", platform(project(":common-bom")))
            add("testAnnotationProcessor", platform(project(":common-bom")))
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        options.compilerArgs += ["-parameters"]
    }

    sub.plugins.withId("maven-publish") {
        sub.publishing {
            repositories {
                maven {
                    if (!repoPath) {
                        throw new GradleException("repoPath is required. ${repoPath}")
                    }
                    url = uri("https://maven.pkg.github.com/${repoPath}")
                    credentials {
                        username = (System.getenv("GITHUB_ACTOR") ?: findProperty("gpr.user"))?.toString()
                        password = (System.getenv("GITHUB_TOKEN") ?: findProperty("gpr.key"))?.toString()
                    }
                }
            }
        }
    }
}
